// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.apache.tools.ant.taskdefs.condition.Os
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.0',
                'org.codehaus.groovy.modules.http-builder:http-builder:0.5.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

String buildNo
String appName
Properties loadedProperties

logging.captureStandardOutput LogLevel.INFO
task initConfig << {

    loadedProperties = new Properties()
    loadedProperties.load(new FileInputStream("defaults.properties"))
    try {
        def configProperties = new Properties()
        configProperties.load(new FileInputStream(config))
        loadedProperties.putAll(configProperties)
        println loadedProperties
    } catch (Exception e) {
        throw new Exception("Invalid configuration file path", e)
    }

    buildNo = UUID.randomUUID().toString()
    mkdir "temp/$buildNo"
}

task copyApk(dependsOn: initConfig) << {
    copy {
        from 'app', 'gradlew', 'gradlew.bat', 'local.properties', 'convert', 'convert.bat'
        into "temp/$buildNo"
        exclude '*.apk', '*.iml', 'build/*'
    }
    copy {
        from('gradle')
        into "temp/$buildNo/gradle"
    }
}

task merginProperties(dependsOn: copyApk) << {
    def buildProps = new Properties()
    buildProps.load(new FileInputStream("temp/$buildNo/src/main/res/raw/config.properties"))
    buildProps.putAll(loadedProperties.findAll { !it.key.startsWith('sign.') })
    buildProps.store(new FileOutputStream("temp/$buildNo/src/main/res/raw/config.properties"), null)
    loadedProperties = buildProps
}

task modifyBuild(dependsOn: merginProperties) << {
    def appname = loadedProperties.getProperty("app.name")
    if (!appname) {
        throw new Exception("Invalid app name")
    }
    appName = appname.replaceAll('[^a-zA-Z0-9 ]', '')
    def resources = fileTree(dir: "temp/$buildNo/src/main/res", include: 'values*/strings.xml')
    resources.each { stringFile ->
        String valuesContent = stringFile.getText('UTF-8')
        valuesContent = valuesContent.replaceAll("<string name=\"app_name\">AppCreator</string>",
                "<string name=\"app_name\">$appName</string>")
        stringFile.write(valuesContent, 'UTF-8')
    }

    File manifestFile = file("temp/$buildNo/src/main/AndroidManifest.xml")
    String manifestContent = manifestFile.getText('UTF-8')
    if(loadedProperties.getProperty('onsd')) {
        manifestContent = manifestContent.replace('android:installLocation="auto"', 'android:installLocation="preferExternal"')
    }
    manifestContent = manifestContent.replaceAll("ua.com.ethereal.appcreator.appid", loadedProperties.getProperty("app.package"))
    manifestFile.write(manifestContent, 'UTF-8')

    File buildGradle = file("temp/$buildNo/build.gradle")
    String buildContent = buildGradle.getText('UTF-8')
    buildContent = buildContent.replace("ua.com.ethereal.appcreator.appid", loadedProperties.getProperty("app.package"))
    buildContent = buildContent.replace("versionName \"1.0\"", "versionName \"" + loadedProperties.getProperty("app.version") + "\"")
    buildGradle.write(buildContent, 'UTF-8')

    copy {
        from loadedProperties.getProperty("app.info")
        into "temp/$buildNo/src/main/res/raw/"
        rename '.*', 'info.txt'
    }

    def splash = loadedProperties.getProperty("splash.image")
    if (!splash) {
        return
    }
    delete "temp/$buildNo/src/main/res/drawable/splash_bg.png"
    copy {
        from splash
        into "temp/$buildNo/src/main/res/drawable/"
        rename '([^\\s]+(\\.(?i)(jpeg|jpg|png|gif|bmp))$)', 'splash_bg.$3'
    }
}

task addPermissions(dependsOn: modifyBuild) << {
    if(!loadedProperties.getProperty("app.permissions")) {
        return
    }
    File manifestFile = file("temp/$buildNo/src/main/AndroidManifest.xml")
    String manifestContent = manifestFile.getText('UTF-8')
    manifestContent = manifestContent.replace('<!--PERMISSIONS_HOLDER-->', new File(loadedProperties.getProperty("app.permissions")).text)
    manifestFile.write(manifestContent, 'UTF-8')
}

task makingKey(dependsOn: addPermissions) << {
    def aliasName = loadedProperties.getProperty("sign.name")
    aliasName = aliasName?aliasName:'appcreator'
    def passwordToKey = loadedProperties.getProperty("sign.password")
    passwordToKey = passwordToKey?passwordToKey:'appcreator'
    def validationYears = loadedProperties.getProperty("sign.years")
    validationYears = validationYears?validationYears:'5000'

    def keyInfo = file("temp/$buildNo/keyinfo.txt")
    String content = keyInfo.getText('UTF-8')
    content = content.replaceAll('_PASS_', passwordToKey)
    keyInfo.write(content, 'UTF-8')

    def keyProps = file("temp/$buildNo/keystore.properties")
    String propsContent = keyProps.getText('UTF-8')
    propsContent = propsContent.replaceAll('_NAME_', aliasName)
    propsContent = propsContent.replaceAll('_PASS_', passwordToKey)
    keyProps.write(propsContent, 'UTF-8')

    def execCommand = './mkey'
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        execCommand = 'mkey.bat'
    }
    exec {
        workingDir "temp/$buildNo"
        commandLine 'keytool', '-genkey', '-v', '-keystore', 'release-key.keystore', '-alias',
                "$aliasName", '-keyalg', 'RSA', '-keysize', '2048', '-validity', "$validationYears"
        standardInput new FileInputStream("temp/$buildNo/keyinfo.txt")
    }

}

task convertIcons(dependsOn: makingKey) << {
    def icon = loadedProperties.getProperty("app.icon")
    if (!icon) {
        throw new Exception("Invalid icon path")
    }

    def sizedirs = [36 : 'src/main/res/drawable-ldpi',
                    48 : 'src/main/res/drawable-mdpi',
                    72 : 'src/main/res/drawable-hdpi',
                    92 : 'src/main/res/drawable-xhdpi',
                    144: 'src/main/res/drawable-xxhdpi']
    sizedirs.each { s, f ->
        def convertCommand = './convert'
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            convertCommand = 'convert.bat'
        }
        exec {
            workingDir "temp/$buildNo"
            commandLine convertCommand, "$icon", "$f/ic_launcher.png"
        }
    }

}

task buildApk(dependsOn: convertIcons) << {
    def execCommand = './gradlew'
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        execCommand = 'gradlew.bat'
    }
    exec {
        workingDir "temp/$buildNo"
        commandLine execCommand, 'assembleRelease'
    }
}

task generateApk(dependsOn: buildApk) << {
    def targetDir = 'generated'
    if(project.hasProperty('target')) {
        targetDir = target
    }
    copy {
        from "temp/$buildNo/build/outputs/apk"
        into targetDir
        include "*-release.apk"
        rename(/(.*)+/, "${appName}.apk")
    }
}

task cleanTemp << {
    if (!buildNo) return
    delete "temp/$buildNo"
}

generateApk.finalizedBy cleanTemp

